buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath JavaVersion.current() == JavaVersion.VERSION_1_8 ? "net.ltgt.gradle:gradle-errorprone-plugin:0.0.16" : "net.ltgt.gradle:gradle-errorprone-javacplugin-plugin:0.3"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.23.0" apply false
}

apply from: "gradle/compile.gradle"

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: JavaVersion.current() == JavaVersion.VERSION_1_8 ? "net.ltgt.errorprone" : "net.ltgt.errorprone-javacplugin"

    group = 'org.zaproxy'

    version '1.10.0'
    ext.versionBC = '1.9.0'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    spotless {
        java {
            licenseHeaderFile "$rootDir/gradle/spotless/license.java"

            googleJavaFormat().style('AOSP')
        }
    }

    dependencies {
        errorprone 'com.google.errorprone:error_prone_core:2.3.1'
    }
}

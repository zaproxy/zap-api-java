plugins {
    id "maven-publish"
    id "signing"
    id "me.champeau.gradle.japicmp" version "0.4.3"
}

sourceSets { examples }

assemble.dependsOn examplesClasses

dependencies {
    // XXX Change to implementation (it's not exposed in public API) when bumping major version.
    api 'org.jdom:jdom:1.1.3'

    examplesImplementation sourceSets.main.output
}

jar {
    manifest {
        attributes 'Implementation-Title': 'ZAP API Client',
        'Implementation-Version': archiveVersion.get(),
        'Main-Class': 'org.zaproxy.clientapi.core.ClientApiMain',
        'Create-Date': new Date().format("yyyy-MM-dd")
    }
}

File clientapiJar(version) {
    def oldGroup = group
    try {
        // https://discuss.gradle.org/t/is-the-default-configuration-leaking-into-independent-configurations/2088/6
        group = "virtual_group_for_japicmp"
        def conf = configurations.detachedConfiguration(dependencies.create("$oldGroup:$name:$version"))
        conf.transitive = false
        return conf.singleFile
    } finally {
        group = oldGroup
    }
}

task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask) {
    group = 'verification'
    description = "Checks artifacts' binary compatibility with latest (released) version '$versionBC'."

    oldClasspath.from(files(clientapiJar(versionBC)))
    newClasspath.from(tasks.named(JavaPlugin.JAR_TASK_NAME))
    onlyBinaryIncompatibleModified.set(true)
    failOnModification.set(true)
    ignoreMissingClasses.set(true)

    richReport {
        destinationDir.set(file("$buildDir/reports/"))
        reportName.set("japi.html")
        addDefaultRules.set(true)
    }
}

check.dependsOn(japicmp)

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

task uberJar(type: Jar) {
    group = 'build'
    description = 'Assembles a jar archive containing the main jar and its dependencies.'
    archiveBaseName.set('zap-api')
    manifest.from jar.manifest
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

build.dependsOn 'uberJar'

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    publications {
        clientApi(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
            
            pom {
                name = 'ZAP API Client'
                description = 'Java implementation to access ZAP API.'
                url = 'https://github.com/zaproxy/zap-api-java'

                organization {
                    name = 'ZAP'
                    url = 'https://www.zaproxy.org/'
                }

                mailingLists {
                    mailingList {
                        name = 'ZAP User Group'
                        post = 'zaproxy-users@googlegroups.com'
                        archive = 'https://groups.google.com/group/zaproxy-users'
                    }
                    mailingList {
                        name = 'ZAP Developer Group'
                        post = 'zaproxy-develop@googlegroups.com'
                        archive = 'https://groups.google.com/group/zaproxy-develop'
                    }
                }

                scm {
                    url = 'https://github.com/zaproxy/zap-api-java'
                    connection = 'scm:git:https://github.com/zaproxy/zap-api-java.git'
                    developerConnection = 'scm:git:https://github.com/zaproxy/zap-api-java.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'psiinon'
                        name = 'Simon Bennetts'
                        email = 'psiinon@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    if (project.hasProperty('signing.keyId')) {
        sign publishing.publications.clientApi
    }
}
